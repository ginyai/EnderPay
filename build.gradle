
buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'org.spongepowered.plugin' version '0.8.1'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

def mcVersion         = "1.12.2" // Always matches the Minecraft version the mod is for.
def majorModVersion   = "1" // Removing items/blocks, changing mechanics, updating to a new Minecraft version.
def majorApiModVersion     = "0" // Changing the order of enums, return types of methods, removing methods
def minorModVersion   = "1" // Adding items, blocks, tile entities, Adding new mechanics., Deprecating public methods.
def patchVersion = "0" // Bugfixes.
def suffix = "" // "-final" - last release for that Minecraft, "-betaX" - Pre-releases, "-rcX"-Release Candidates
version = mcVersion+"-"+majorModVersion+"."+majorApiModVersion+"."+minorModVersion+"."+patchVersion+suffix

def env = System.getenv()
def minecraftModdingEnv = env['MinecraftModdingEnv'] != null ? env['MinecraftModdingEnv'].trim() : "dev"

group = "com.kamildanak.minecraft.enderpay" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "enderpay"

targetCompatibility = "1.8"
sourceCompatibility = "1.8"

minecraft {
    version = "1.12.2-14.23.4.2705"
    mappings = "snapshot_20180609"

    if (file("../run").exists()) {
        runDir = "../run"
    } else {
        runDir = "run"
    }
    replace "{@enderPayVersion}":project.version
    replace "{@mcVersion}":mcVersion
}

repositories {
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        url "http://kamildanak.com/maven/"
    }
}

//if ( minecraftModdingEnv == "production" ) {
//    dependencies {
//        compile name: "foamflower", group: "com.kamildanak.minecraft.foamflower", version: "1.12.2-1.0.0.0-beta1", classifier: "deobf"
//    }
//} else {
//    dependencies {
//        compile project(':Foamflower')
//    }
//}

dependencies{
    compile 'org.spongepowered:spongeforge:1.12.2-2705-7.1.0-BETA-3361:dev'
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'
                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}
tasks.build.dependsOn('sourceJar', 'deobfJar')

artifacts {
    archives deobfJar
    archives sourceJar
    archives jar
}
//
//uploadArchives {
//    repositories {
//        mavenDeployer {
//            repository(url: "file://localhost/"+projectDir+"/../local-repo/")
//        }
//    }
//}
